plugins {
    id 'idea'
    id 'java'
    id 'maven-publish'
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT'
}
base {
    archivesName = "${mod_id}-common-mojmap"
}
sourceSets {
    api {
        java
        resources
        compileClasspath += sourceSets.main.compileClasspath
    }
    main {
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
    }
    testmod {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }
}
configurations {
    api
}
minecraft {
    version(minecraft_version)
    if(file("src/main/resources/${mod_id}.accesswidener").exists()){
        accessWideners(file("src/main/resources/${mod_id}.accesswidener"))
    }
}
dependencies {
    testmodCompileOnly sourceSets.main.output

    compileOnly group:'org.spongepowered', name:'mixin', version:'0.8.5'
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'

    compileOnly group:'com.google.auto.service', name:'auto-service', version:'1.0.1'
    annotationProcessor group:'com.google.auto.service', name:'auto-service', version:'1.0.1'
}

tasks.register('apiJar', Jar) {
    archiveClassifier = "api"
    from sourceSets.api.allSource
}

artifacts {
    api apiJar
}

assemble.dependsOn apiJar

compileJava {
    source(sourceSets.api.allSource)
}
tasks.withType(Javadoc).configureEach {
    source(sourceSets.api.allSource)
}
tasks.named("sourcesJar", Jar) {
    from(sourceSets.api.allSource)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
            artifact(apiJar) {
                builtBy apiJar
                classifier 'api'
            }
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
        maven {
            name = "merchantpug"
            url = "https://maven.merchantpug.net/releases"
            credentials {
                username System.getenv('MAVEN_USERNAME')
                password System.getenv('MAVEN_PASSWORD')
            }
            authentication {
                create("basic", BasicAuthentication)
            }
        }
    }
}