import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'fabric-loom'
}
base {
    archivesName = "${mod_id}-common-intermediary"
}

evaluationDependsOn ':common'

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
    mappings loom.officialMojangMappings()
}

// Credits to Witixin. https://gist.github.com/Witixin1512/bbb8484523ea6a0e8440f17621f89f95
tasks.register("remapCommonJar", RemapJarTask,  task -> {
    final AbstractArchiveTask jarTask = project(":common").getTasks().named("jar", AbstractArchiveTask.class).get();
    task.dependsOn(jarTask)

    task.getInputFile().convention(jarTask.getArchiveFile())
    task.dependsOn(project(":common").getTasks().named("jar"))
    task.archiveFileName.set(archiveFileName.get().replace(".jar", "-intermediary.jar"))
    task.targetNamespace.set("intermediary")

    remapperIsolation = true
})

// Credits to Witixin. https://gist.github.com/Witixin1512/bbb8484523ea6a0e8440f17621f89f95
tasks.register("remapApiJar", RemapJarTask,  task -> {
    final AbstractArchiveTask jarTask = project(":common").getTasks().named("apiJar", AbstractArchiveTask.class).get();
    task.dependsOn(jarTask)

    task.getInputFile().convention(jarTask.getArchiveFile())
    task.dependsOn(project(":common").getTasks().named("apiJar"))
    task.archiveFileName.set(archiveFileName.get().replace(".jar", "-api-intermediary.jar"))
    task.targetNamespace.set("intermediary")

    remapperIsolation = true
})

tasks.named('build').configure {
    dependsOn.clear()
    dependsOn('remapCommonJar')
    dependsOn('remapApiJar')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            artifact(remapCommonJar) {
                builtBy remapCommonJar
                classifier 'intermediary'
            }
            artifact(remapApiJar) {
                builtBy remapApiJar
                classifier 'api-intermediary'
            }
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
        maven {
            name = "merchantpug"
            url = "https://maven.merchantpug.net/releases"
            credentials {
                username System.getenv('MAVEN_USERNAME')
                password System.getenv('MAVEN_PASSWORD')
            }
            authentication {
                create("basic", BasicAuthentication)
            }
        }
    }
}
